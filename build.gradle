plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version "2.0.0"
    id 'org.cyclonedx.bom' version "1.10.0"
    id 'org.sonarqube' version "4.0.0.2929"
    id 'io.github.sgtsilvio.gradle.javadoc-links' version "0.8.0"
}

sonarqube {
  properties {
    property "sonar.projectKey", "pjfanning_excel-streaming-reader"
    property "sonar.organization", "pjfanning"
    property "sonar.host.url", "https://sonarcloud.io"
    // the plugin seems to not detect our non-standard build-layout
    property "sonar.junit.reportPaths", "$projectDir/build/test-results/test"
  }
}

group = 'com.github.pjfanning'
version = '5.0.3-SNAPSHOT'

sourceCompatibility = 1.8

java {
    withJavadocJar()
    withSourcesJar()
    registerFeature('poiSharedStrings') {
        usingSourceSet(sourceSets.main)
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repository.apache.org/content/groups/staging'
    }
}

ext {
    poiVersion = '5.4.0'
    poiSharedStringsVersion = '2.9.0'
    slf4jVersion = '2.0.16'
}

dependencies {
    api "org.apache.poi:poi:$poiVersion"
    api "org.apache.poi:poi-ooxml:$poiVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation 'commons-io:commons-io:2.17.0'
    implementation 'org.apache.commons:commons-compress:1.27.1'
    poiSharedStringsImplementation "com.github.pjfanning:poi-shared-strings:$poiSharedStringsVersion"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.nanohttpd:nanohttpd:2.3.1'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
    testRuntimeOnly 'org.apache.logging.log4j:log4j-to-slf4j:2.24.1'
}

test {
    options {
        systemProperties(System.getProperties())
    }
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'com.github.pjfanning.excelstreamingreader')
    }
}

compileTestJava.dependsOn('copyLicenseToBuildResources')
jar.dependsOn('copyLicenseToBuildResources')
javadoc.dependsOn('copyLicenseToBuildResources')

nexusPublishing {
    repositories {
        sonatype()
    }
}

publish.dependsOn cyclonedxBom

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'excel-streaming-reader'
                description = 'Streaming Excel reader'
                url = 'https://github.com/pjfanning/excel-streaming-reader'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'monitorjbl'
                        name = 'Taylor Jones'
                        email = 'monitorjbl@gmail.com'
                    }
                    developer {
                        id = 'pjfanning'
                        name = 'PJ Fanning'
                        url = 'https://github.com/pjfanning'
                    }
                }
                scm {
                    url = 'https://github.com/pjfanning/excel-streaming-reader'
                    connection = 'scm:git://github.com/pjfanning/excel-streaming-reader.git'
                    developerConnection = 'scm:git://github.com/pjfanning/excel-streaming-reader.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

tasks.register('copyLicenseToBuildResources', Copy) {
    from layout.projectDirectory.file("LICENSE")
    into layout.buildDirectory.dir("resources/main/META-INF")
}
