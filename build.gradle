plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'org.cyclonedx.bom' version "1.7.1"
    id 'org.sonarqube' version "3.4.0.2513"
}

sonarqube {
  properties {
    property "sonar.projectKey", "pjfanning_excel-streaming-reader"
    property "sonar.organization", "pjfanning"
    property "sonar.host.url", "https://sonarcloud.io"
    // the plugin seems to not detect our non-standard build-layout
    property "sonar.junit.reportPaths", "$projectDir/build/test-results/test"
  }
}

group = 'com.github.pjfanning'
version = '4.0.2-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    //maven { url 'https://repository.apache.org/content/repositories/staging' }
}

dependencies {
    api 'org.apache.poi:poi:5.2.3'
    api 'org.apache.poi:poi-ooxml:5.2.3'
    implementation 'com.github.pjfanning:poi-shared-strings:2.5.3'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'commons-io:commons-io:2.11.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.nanohttpd:nanohttpd:2.3.1'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.36'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-to-slf4j:2.18.0'
}

test {
    options {
        systemProperties(System.getProperties())
    }
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'com.github.pjfanning.excelstreamingreader')
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publish.dependsOn cyclonedxBom

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'excel-streaming-reader'
                description = 'Streaming Excel reader'
                url = 'https://github.com/pjfanning/excel-streaming-reader'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'monitorjbl'
                        name = 'Taylor Jones'
                        email = 'monitorjbl@gmail.com'
                    }
                    developer {
                        id = 'pjfanning'
                        name = 'PJ Fanning'
                    }
                }
                scm {
                    url = 'https://github.com/pjfanning/excel-streaming-reader'
                    connection = 'scm:git://github.com/pjfanning/excel-streaming-reader.git'
                    developerConnection = 'scm:git://github.com/pjfanning/excel-streaming-reader.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
